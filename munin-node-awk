function mpr(content) {
	print content |& Service
}
function do_list() {
	mpr("load")
}

function load_average(args) {
	if (args == "conf") {
		mpr("graph_title Load average");
		mpr("graph_args --base 1000 -l 0");
        	mpr("graph_vlabel load");
		mpr("graph_scale no");
		mpr("graph_category system");
        	mpr("load.label load");
        	mpr("graph_info The load average of the machine describes how many processes are in the run-queue (scheduled to run \"immediately\").");
		mpr("load.info 5 minute load average");
		return;
	} else {
		getline <"/proc/loadavg"
		mpr("load.value " $2)
		close("/proc/loadavg");
		return
	}
}

BEGIN {
	Service = "/inet/tcp/4919/0/0";
	do {
		mpr("# munin node at luke.awk")
		while ((Service |& getline)>0) {
			print "got: "$0
			if (/^quit/) {
				mpr("bye!");
				break;
			} else if (/^list/) {
				do_list()
			} else if (/^config load/) {
				load_average("conf");
				mpr(".");
			} else if (/^fetch load/) {
				load_average();
				mpr(".");
			} else {
				mpr("#unknown command");
			}
		};
		close(Service);
	} while(1);
}
